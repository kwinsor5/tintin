#class prompt open;

#nop required: bot_main.tt;

#unvar SCALE10;
#var SCALE10[0]  {----------};
#var SCALE10[1]  {<fbb>o<088>---------};
#var SCALE10[2]  {<fbb>o<fcb>o<088>--------};
#var SCALE10[3]  {<fbb>o<fcb>o<fdb>o<088>-------};
#var SCALE10[4]  {<fbb>o<fcb>o<fdb>o<feb>o<088>------};
#var SCALE10[5]  {<fbb>o<fcb>o<fdb>o<feb>o<ffb>o<088>-----};
#var SCALE10[6]  {<fbb>o<fcb>o<fdb>o<feb>o<ffb>o<efb>o<088>----};
#var SCALE10[7]  {<fbb>o<fcb>o<fdb>o<feb>o<ffb>o<efb>o<dfb>o<088>---};
#var SCALE10[8]  {<fbb>o<fcb>o<fdb>o<feb>o<ffb>o<efb>o<dfb>o<cfb>o<088>--};
#var SCALE10[9]  {<fbb>o<fcb>o<fdb>o<feb>o<ffb>o<efb>o<dfb>o<cfb>o<bfb>o<088>-};
#var SCALE10[10] {<fbb>o<fcb>o<fdb>o<feb>o<ffb>o<efb>o<dfb>o<cfb>o<bfb>o<afb>o<088>};


#highlight {{H:|HP:}}{<fac>};
#highlight {{S:|SM:}}{<adf>};
#highlight {{M:|MV:}}{<afa>};

#function {scale_xp}
{

    #local _min {%1};
    #local _max {%2};
    #local _avg {%3};
    #local _current {0};
    #math {_current} {((($_avg-$_min)*100)/($_max-$_min))/ 10};
    #if {$_current > 10}
    {
        #local _current 10;
    };
    #if {$_current < 0}
    {
        #local _current 0;
    };

    #return $SCALE10[$_current];
}


#action {[HP: %1/%2 S: %3/%4 MV: %5/%6]}
{
    #local _mana {};
    #local _health {};
    #local _moves {};

    #var {my[hp][current]} {%1};
    #var {my[hp][max]} {%2};
    #var {my[sm][current]} {%3};
    #var {my[sm][max]} {%4};
    #var {my[mv][current]} {%5};
    #var {my[mv][max]} {%6};

    #var {my[hp][graph]} {@graph{%1;%2}};
    #var {my[sm][graph]} {@graph{%3;%4}};
    #var {my[mv][graph]} {@graph{%5;%6}};

    #if {$PROMPT[is_enabled] == 1}
    {

        #format {hp}		{%+4s} {@colscale{$my[hp][current];$my[hp][max]}};
        #format {hp_max}	{%+4s} {<128>$my[hp][max]<088>};
        #format {sm}		{%+4s} {@colscale{$my[sm][current];$my[sm][max]}};
        #format {sm_max}	{%+4s} {<128>$my[sm][max]<088>};
        #format {mv}		{%+4s} {@colscale{$my[mv][current];$my[mv][max]}};
        #format {mv_max}	{%+4s} {<128>$my[mv][max]<088>};
        #showme {H:$my[hp][graph] S:$my[sm][graph] M:$my[mv][graph]}{$SCREEN[prompt][first_row]}{$SCREEN[prompt][first_col]};
        #showme {HP:$hp/$hp_max SM:$sm/$sm_max MV:$mv/$mv_max}{$SCREEN[prompt][second_row]}{$SCREEN[prompt][first_col]};

    };
    #else
    {
        #showme {H:$my[hp][graph] S:$my[sm][graph] M:$my[mv][graph]}{0}{1};
    };

    #if {$bot && !$halt && !$BOT[foundmob] && !$BOT[foundplayer] && !$BOT[current_engaged]}
    {
        #var {BOT[foundplayer]} 1;
    };

    @follow_target{};
};

#function {_update_xp_prompt}
{
    #if {$PROMPT[is_enabled] == 0}
    {
        #RETURN {#NOP;};
    };

    #local _min_xp {$XP_TRACKER[min]};
    #local _max_xp {$XP_TRACKER[max]};
    #local _avg_xp {$XP_TRACKER[current_avg]};

    #local _xp_bar {};

    #FORMAT {_xp_bar} {%cMIN XP:%+6s @scale_xp{{$_min_xp}{$_max_xp}{$_avg_xp}} %cMAX XP:%+6s}{<abc>}{$_min_xp}{<aff>}{$_max_xp};

    #show {$_xp_bar<fff>}{$SCREEN[prompt][fifth_row]}{$SCREEN[prompt][first_col]};
    #RETURN {#NOP;};
};

@PUBSUB_SUBSCRIBE{screen_changed; _update_xp_prompt;};

#function {graph}
{
	#regex {%1} {%d} {
		#math per {100.0*%1/%2};
		#format {per} {%d} {$per};
		#if {$per > 100} {#var graph {<006>   $per%%   <088>}};
		#if {$per == 100} {#var graph {<008><AFB>   $per%%   <088>}};
		#if {$per < 100} {#var graph {<008><BFB>    $per%%  <088> }};
		#if {$per < 90}	{#var graph {<008><CFB>    $per%% <070>  <088>}};
		#if {$per < 80} {#var graph {<008><DFB>    $per%%<070>   <088>}};
		#if {$per < 70} {#var graph {<008><EFB>    $per<070>%%   <088>}};
		#if {$per < 60} {
			#format {per_1} {%.1s} {$per};
			#format {per_2} {%r} {$per};
			#format {per_2} {%.1s} {$per_2};
			#var graph {<008><FFB>    ${per_1}<070>${per_2}%%   <088>}};
		#if {$per < 50} {#var graph {<008><FEB>    <070>$per%%   <088>}};
		#if {$per < 40} {#var graph {<008><FDB>   <070> $per%%   <088>}};
		#if {$per < 30} {#var graph {<508><FCB>  <570>  $per%%   <088>}};
		#if {$per < 20} {#var graph {<508><FBB> <570>   $per%%   <088>}};
		#if {$per < 10} {#var graph {<570>     $per%%   <088>}}
	} {
		#format {var} {%.3s} {%1};
		#if {"$var" == "bet" || "$var" == "hea"} {
			#var graph {<006>          <088>}
		}; #elseif {"$var" == "per"} {
			#var graph {<008><AFA>          <088>}
		}; #elseif {"$var" == "bru"} {
			#var graph {<008><BEA>        <088>  }
		}; #elseif {"$var" == "ble"} {
			#var graph {<008><DBA>    <088>      }
		}; #elseif {"$var" == "eme" || "$var" == "alm"} {
			#var graph {<008><FAA> <088>         }
		}; #else {
			#var graph {          }
		}
	};

        #return $graph
}

#function {colscale} {
	#math per {100*%1/%2};
        #switch {1} {
                #case {$per > 100}	{#return <168>%1<088>};
                #case {$per > 90}	{#return <afb>%1<088>};
                #case {$per > 80}	{#return <cfb>%1<088>};
                #case {$per > 70}	{#return <dfb>%1<088>};
                #case {$per > 60}	{#return <efb>%1<088>};
                #case {$per > 50}	{#return <ffb>%1<088>};
                #case {$per > 40}	{#return <feb>%1<088>};
                #case {$per > 30}	{#return <599><fdb>%1<088>};
                #case {$per > 20}	{#return <599><fcb>%1<088>};
                #case {$per > 10}	{#return <599><fbb>%1<088>};
                #case {$per >= 0}	{#return <599><faa>%1<088>};
                #default 	{#return <088>%1};
        }
}

#action {^%1>$}
{
    #if {$PROMPT[is_enabled] == 1}
    {
        #RETURN;
    };

    @follow_target{};
}


#class prompt close;
